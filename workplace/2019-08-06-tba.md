---
title: On the value of writing fully polymorphic code
subtitle: test
author: Pawel Szulc
tags: haskell, polymorphic, mtl
---

# TL;DR

This post if for people who:

* have general understanding of Haskell's syntax, yet they haven't written a single program in Haskell (beyond hello-world)
* want to see how programs in Haskell are written
* want to finally grasp why people get so excited with two concepts: composability and ad-hoc polymorphism

In this post we will:

1. write a simple application command line application
2. realize that being pure just for the sake of it does not bring any value
3. refactor the application to be fully polymoprhic and see a lot of value in that approach

The source code of the mentioned application is available on github under <https://github.com/rabbitonweb/haskell-weather-app>.

# The application

We will write a simple application in Haskell. This will be a weather app, that works more or less like this:

```
> run
Using service at host:8080

What is the next city?
Wroclaw
Forecast for city Wroclaw is Temperature = 7 Celsius
Hottest city found so far: Just (City "Wroclaw",Temperature = 7 Celsius)

What is the next city?
Cadiz
Forecast for city Cadiz is Temperature = 35 Celsius
Hottest city found so far: Just (City "Cadiz",Temperature = 35 Celsius)

What is the next city?
London
Forecast for city London is Temperature = 15 Celsius
Hottest city found so far: Just (City "Cadiz",Temperature = 35 Celsius)

What is the next city?
blablablabbla
UnknownCity "blablablabbla"
> 
```

# Building Blocks

The application looks trivial, but its complex enough to talk about composability. In order to write this program we need to solve some independed issues. Particularly we need to:

1. have an ability to fetch host and port from the configuration - in order to talk to the external weather service
2. be able to connect to external weather service - in order to request weather forecasts
3. be able to interact with the user (input and output)
4. parse input (city name) to valid object (`City`) while dealing with potential errors
5. store all the requests (and their results) in local cache - in order to print the hottest city found so far

This is already quite a lot. Hopefull once done, we will be able to derive the final program from all those subsolutions. That's in fact the very definition of **composability** and rumor has it, that Haskell very good at it. Let's wait and see.

### 1. Ability to fetch host and port

Configuration will be stored in the `Config` object

```haskell
data Config = Config {
    cHost :: String
  , cPort :: Int
  }
```

To which we need to have access to in order to fetch host and port

```haskell
-- from transformers package
import Control.Monad.Trans.Reader 

-- almost classical usage of Reader monad
fetchHost :: Reader Config String
fetchHost = asks cHost

fetchPort :: Reader Config Int
fetchPort = asks cPort
```


### 2. Ability to connect to external weather service

Data types representing `Forecast`

```haskell
-- simple types representing Forecast
data TempUnit = Fahrenheit | Celsius deriving Show
data Temperature = Temperature Int TempUnit
data Forecast = Forecast Temperature

-- Show instances to display Forecast a bit nicer
instance Show Temperature where
  show (Temperature v u) = (show v) ++ " " ++ (show u)

instance Show Forecast where
  show (Forecast temp) = "Temperature = " ++ (show temp)
```

and simple `City` representation

```haskell
data City = City String
```

We need a function with following signature 

```haskell
forecastIO :: String -> Int -> City -> IO Forecast
```

This function should take host and port of a particular weather service, connect to it and fetch the weather information. 
While writing this function would be a lot of fun, for the purpose of this blog post, we will stick with a dummy implementation.

```haskell
forecastIO :: String -> Int -> City -> IO Forecast
forecastIO _ _ (City "Wroclaw") = return $ Forecast $ Temperature 7 Celsius
forecastIO _ _ (City "Cadiz")   = return $ Forecast $ Temperature 35 Celsius
forecastIO _ _ (City "London")  = return $ Forecast $ Temperature 15 Celsius
forecastIO _ _ _                = fail "City not recognized"
```

### 3. Ability to interact with the user

To interact with the user we can use build-in functions: `putStrLn` and `getLine`. We can additionally write helper function `askQuestion`

```haskell
askQuestion :: String -> IO String
askQuestion question = do
  putStrLn question
  getLine
```

### 4. Ability to parse input (with error handling)

Normally we would go with a function like

```haskell
toCity :: String -> City
```

because however we need to handle errors (incorrect `String` input), function needs to be adjusted

```haskell
toCity :: String -> Either Error City
```

One more time the signature is more important then the actuall implementation (in real life this would be something a bit more complex)

```haskell
toCity :: String -> Either Error City
toCity "Wroclaw" = Right $ City "Wroclaw"
toCity "Cadiz" = Right $ City "Cadiz"
toCity "London" = Right $ City "London"
toCity name = Left $ UnknownCity name
```

### 5. Ability to store all the requests (and their results)

The application should store latest `Forecast` result in local `Cache`

```haskell
-- Map from containers package
import qualified Data.Map.Strict as M

type Cache = M.Map City Forecast
```

We need to know how to update the cache

```haskell
import           Control.Monad.Trans.State

-- another classic, usage of State monad
updateForecast :: City -> Forecast -> State Cache ()
updateForecast city forecast = modify $ M.insert city forecast
```

And finally we need to select hottest city found so far from that local cache

```haskell
import           Data.Tuple

hottestCity :: State Cache (Maybe (City, Forecast))
hottestCity = do
  reqs <- get
  return $ Just $ swap $ maximum (fmap swap (M.assocs reqs))
```

The `maximum` requires both `Forcast` and `City` to have instance of `Ord` typeclass

```haskell
data City = City String  deriving (Show, Eq, Ord)

data TempUnit = Fahrenheit | Celsius deriving (Show, Eq, Ord)
data Temperature = Temperature Int TempUnit deriving (Eq, Ord)
data Forecast = Forecast Temperature deriving (Eq, Ord)
```

Which is an obviously wrong implementation  - will only work for `Forecast` returned with the same `TempUnit`. But as mentioned few times already in this post, we are interested a bit more with the overall function signatures, less with the 100% correctness of their implementation - which we consider a detail at this point.

# Putting it all together

Let's sum up all the building blocks we got so far

```haskell
-- Ability to fetch host and port
fetchHost :: Reader Config String
fetchPort :: Reader Config Int

-- Ability to connect to external weather service
forecastIO :: String -> Int -> City -> IO Forecast

-- Ability to interact with the user
askQuestion :: String -> IO String

-- Ability to parse input (with error handling)
toCity :: String -> Either Error City

-- Ability to store all the requests (and their results)
updateForecast :: City -> Forecast -> State Cache ()
hottestCity :: State Cache (Maybe (City, Forecast))
```

Plug it 

```haskell
askAndFetch :: ??? ()
askAndFetch = do
  host <- fetchHost
  port <- fetchPort
  cityName <- askQuestion "What is the next city?"
  city <- toCity cityName
  forecast <- forecastIO host port city
  updateForecast city forecast
  putStrLn $ "Forecast for city " ++ cityName ++ " is " ++ (show forecast)
  hottest <- hottestCity
  putStrLn $ "Hottest city found so far: " ++ (show hottest)

runApp :: StateT Cache (ReaderT Config (ExceptT Error IO)) ()
runApp = do
  host <- fetchHost
  port <- fetchPort
  putStrLn $ "Using service at " ++ host ++ ":" ++ show port + "\n"
  forever askAndFetch
```
